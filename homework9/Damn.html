<script id="vertex" type="x-shader/x-vertex">
    precision mediump float;
    
    attribute vec3 aVertexPosition;
    attribute vec3 aNormal;
    attribute vec2 aTextureCoord;

    uniform mat4 model;
    uniform mat4 view;
    uniform mat4 projection;
    uniform mat3 normalModel;

    varying vec2 TexCoord;
    varying vec3 Normal;
    varying vec3 FragPos;
    
    void main(void) {

        TexCoord = aTextureCoord;
        Normal = normalize(normalModel * aNormal);
        FragPos = vec3(view * model * vec4(aVertexPosition, 1.0));
    
        gl_Position = projection * view * model * vec4(aVertexPosition, 1.0);
    }
</script>
<script id="fragment" type="x-shader/x-fragment">
    #define MAX_LIGHT 1
    #extension GL_OES_standard_derivatives : enable
    #extension GL_EXT_shader_texture_lod : enable
    precision mediump float;

    struct Material {
        vec4 ambient;
        vec4 diffuse;
        vec4 specular;
        vec4 emission;
        float shininess;

        sampler2D diffuse_texture;
        sampler2D specular_texture;
        sampler2D normal_texture;
        sampler2D parallax_texture;
        sampler2D emission_texture;
    };

    struct Light {
        vec4 position;
        vec4 direction;

        vec4 ambient;
        vec4 diffuse;
        vec4 specular;

        float cutOff;
        float outerCutOff;
        float exponent;

        float constant;
        float linear;
        float quadratic;

        bool enable;
    };
    
    varying vec2 TexCoord;
    varying vec3 Normal;
    varying vec3 FragPos;

    uniform bool useLighting;
    uniform bool useDiffuseTexture;
    uniform bool useSpecularTexture;
    uniform bool useNormalTexture;
    uniform bool useParallaxTexture;
    uniform bool useEmissionTexture;
    
    uniform mat4 view;
    uniform float depthScale;

    uniform Material material;
    uniform Light lights[MAX_LIGHT];

    // 如果小於 0.001 的數值就當作是 0。
    bool isZero(float number) {
        return abs(number) <= 1e-3;
    }

    // 轉置矩陣
    mat3 transpose(in mat3 matrix) {
        vec3 r0 = matrix[0];
        vec3 r1 = matrix[1];
        vec3 r2 = matrix[2];

        mat3 result = mat3(
            vec3(r0.x, r1.x, r2.x),
            vec3(r0.y, r1.y, r2.y),
            vec3(r0.z, r1.z, r2.z)
        );

        return result;
    }

    mat3 cotangent_frame(vec3 N, vec3 p, vec2 uv) {
        // get edge vectors of the pixel triangle
        vec3 dp1 = dFdx(p);
        vec3 dp2 = dFdy(p);
        vec2 duv1 = dFdx(uv);
        vec2 duv2 = dFdy(uv);

        // solve the linear system
        vec3 dp2perp = cross(dp2, N);
        vec3 dp1perp = cross(N, dp1);
        vec3 T = dp2perp * duv1.x + dp1perp * duv2.x;
        vec3 B = dp2perp * duv1.y + dp1perp * duv2.y;

        // construct a scale-invariant frame
        float invmax = inversesqrt(max(dot(T, T), dot(B, B)));
        return mat3(T * invmax, B * invmax, N);
    }

    vec2 parallax_uv(sampler2D tex_depth, vec2 uv, vec3 view_dir, int num_layers, float depth_scale) {
        float layer_depth = 1.0 / float(num_layers);
        float current_layer_depth = 0.0;
        vec2 delta_uv = view_dir.xy * depth_scale / (view_dir.z * float(num_layers));
        vec2 current_uv = uv;

        float depth_from_texture = texture2D(tex_depth, current_uv).r;
        for (int i = 0; i < 127; i++) {
            if (i >= num_layers) {
                break;
            }
            current_layer_depth += layer_depth;
            current_uv -= delta_uv;
            depth_from_texture = texture2D(tex_depth, current_uv).r;
            if (depth_from_texture < current_layer_depth) {
                break;
            }
        }

        vec2 prev_uv = current_uv + delta_uv;
        float next = depth_from_texture - current_layer_depth;
        float prev = texture2D(tex_depth, prev_uv).r - current_layer_depth + layer_depth;
        float weight = next / (next - prev);
        return mix(current_uv, prev_uv, weight);
    }

    vec3 perturb_normal(mat3 TBN, vec2 TexCoord) {
        // 透過 TexCoord 來取得對應的 Normal index
        vec3 texture_normal = texture2D(material.normal_texture, TexCoord).xyz;
        
        // 將 0~255 值映射到 -1 與 1 之間。
        // texture_normal = texture_normal * 2.0 - 1.0;
        texture_normal = texture_normal * 255.0 / 127.0 - 128.0 / 127.0;

        // 上下顛倒
        texture_normal.y = -texture_normal.y;

        return TBN * texture_normal;
    }

    vec4 calcLight(Light light, Material material, vec3 norm, vec4 texel, vec2 texcoord) {
        vec4 lightPos = view * light.position;
        vec3 lightDir = normalize(lightPos.xyz - FragPos.xyz);
        float diff = max(dot(norm, lightDir), 0.0);

        vec3 ViewDir = normalize(-FragPos);
        vec3 halfway = normalize(ViewDir + lightDir);

        float shininess = material.shininess;
        if (useSpecularTexture) {
            shininess = shininess * texture2D(material.specular_texture, texcoord).r;
        }

        float spec = pow(max(dot(norm, halfway), 0.0), shininess);

        float distance = length(lightPos.xyz - FragPos.xyz);
        float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * distance * distance);
            
        vec4 ambient = light.ambient * material.ambient * texel;
        vec4 diffuse = light.diffuse * diff * material.diffuse * texel;
        vec4 specular = light.specular * spec * material.specular;
    
        return ambient + (diffuse + specular) * attenuation;
    }

    void main(void) {

        // Initial
        mat3 TBN = mat3(1.0);
        vec2 texture_coordinate = vec2(0.0);
        vec4 texel = vec4(0.0);

        vec3 ViewDir = normalize(-FragPos);

        // 如果有啟用 凹凸貼圖 => 重新計算材質座標
        int parallaxStep = 64;
        if (useParallaxTexture) {
            texture_coordinate = parallax_uv(material.parallax_texture, TexCoord, -ViewDir, parallaxStep, depthScale);
        } else {
            texture_coordinate = TexCoord;
        }

        // 如果沒有啟用 漫射貼圖 => 使用純色(白色)
        if (useDiffuseTexture) {
            texel = texture2D(material.diffuse_texture, texture_coordinate);
        } else {
            texel = vec4(1.0, 1.0, 1.0, 1.0);
        }

        // 是否開啟光照
        if (!useLighting) {
            gl_FragColor = texel;
        } else {
            vec3 illumination = vec3(0.0);
            vec3 norm = Normal;
            mat3 TBN = cotangent_frame(norm, -ViewDir, texture_coordinate);
            
            if (useParallaxTexture) {
                // TBN 是正交矩陣，它的反矩陣會等於它的轉置矩陣。
                mat3 TBN_inverse = transpose(TBN);
                texture_coordinate = parallax_uv(material.parallax_texture, TexCoord, normalize(TBN_inverse * (-ViewDir)), parallaxStep, depthScale);
                TBN = cotangent_frame(norm, -ViewDir, texture_coordinate);
                if (useDiffuseTexture) {
                    texel = texture2D(material.diffuse_texture, texture_coordinate);
                }
            }

            if (useNormalTexture) {
                norm = perturb_normal(TBN, TexCoord);
            }
            norm = normalize(norm);

            for (int i = 0; i < MAX_LIGHT; i++) {
                
                if (!lights[i].enable) {
                    continue;
                }

                illumination += clamp(calcLight(lights[i], material, norm, texel, texture_coordinate).rgb, 0.0, 1.0);
                // illumination = texel.rgb;
            }

            if (useEmissionTexture) {
                illumination += texture2D(material.emission_texture, texture_coordinate).rgb;
            }

            if(texel.a < 0.1) {
                discard;
            }
            gl_FragColor = vec4(clamp(illumination, 0.0, 1.0), texel.a);
        }
    }
</script>